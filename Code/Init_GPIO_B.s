GPIO_PORTB_DATA_ALL	EQU 0x400053FC
GPIO_PORTB_DATA_OUT	EQU 0x400053C0
GPIO_PORTB_DATA_IN	EQU 0x4000503C
GPIO_PORTB_DIR_R 	EQU 0x40005400
GPIO_PORTB_AFSEL_R 	EQU 0x40005420
GPIO_PORTB_DEN_R 	EQU 0x4000551C
GPIO_PORTB_PUR_R 	EQU 0x40005510
GPIO_PORTB_IEV 		EQU 0x4000540C
GPIO_PORTB_IMSK 	EQU 0x40005410
GPIO_PORTB_MIS 		EQU 0x40005418
GPIO_PORTB_ICR 		EQU 0x4000541C
SYSCTL_RCGCGPIO		EQU 0x400FE608
SYSCTL_PRGPIO 		EQU 0x400FEA08

			AREA INIT_GPIOB_CODE, CODE, READONLY
			THUMB
			EXPORT INIT_GPIOB
				
INIT_GPIOB	PROC
			PUSH 	{R0, R1}		
			; Turn On Port B Clock
			LDR 	R1, = SYSCTL_RCGCGPIO
			LDR 	R0, [R1]
			ORR 	R0, R0, #0x02 
			STR 	R0, [R1]

			; Check Port Ready Status
			LDR 	R1, = SYSCTL_PRGPIO
CHK_PRT		LDR 	R0, [R1]
			ANDS  	R0, #0x02
			BEQ 	CHK_PRT
			
			; Set Directions
			LDR 	R1, =GPIO_PORTB_DIR_R
			MOV 	R0, #0xF0
			STR 	R0, [R1]
			
			; Set as Regular GPIO
			LDR 	R1, =GPIO_PORTB_AFSEL_R
			MOV 	R0, #0x00
			STR 	R0, [R1]

			; Enable Digital Port
			LDR 	R1, =GPIO_PORTB_DEN_R
			MOV 	R0, #0xFF
			STR 	R0, [R1]
			
			; Set Output Pins as High (LEDs are Off)
			LDR 	R1, =GPIO_PORTB_DATA_OUT
			MOV 	R0, #0xE0
			STR		R0, [R1]
			
			; Set Pull-Up
			LDR 	R1, =GPIO_PORTB_PUR_R
			MOV 	R0, #0x0F
			STR 	R0, [R1]
			
			POP 	{R0, R1}			
			BX 		LR
			ENDP
				
			ALIGN
			END