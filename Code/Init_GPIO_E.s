GPIO_PORTE_DATA_ALL	EQU 0x400243FC
GPIO_PORTE_DATA_OUT	EQU 0x400240C0
GPIO_PORTE_DATA_IN	EQU 0x40024030	;1100.00
GPIO_PORTE_DIR_R 	EQU 0x40024400
GPIO_PORTE_AFSEL_R 	EQU 0x40024420
GPIO_PORTE_AMSEL_R 	EQU 0x40024528
GPIO_PORTE_DEN_R 	EQU 0x4002451C
GPIO_PORTE_PUR_R 	EQU 0x40024510
GPIO_PORTE_OD_R 	EQU 0x4002450C
GPIO_PORTE_IEV 		EQU 0x4002440C
GPIO_PORTE_IMSK 	EQU 0x40024410
GPIO_PORTE_MIS 		EQU 0x40024418
GPIO_PORTE_ICR 		EQU 0x4002441C
SYSCTL_RCGCGPIO		EQU 0x400FE608
SYSCTL_PRGPIO 		EQU 0x400FEA08

			AREA INIT_GPIOE_CODE, CODE, READONLY
			THUMB
			EXPORT INIT_GPIOE
				
INIT_GPIOE	PROC
			PUSH 	{R0, R1}
			; Turn On Port E Clock
			LDR 	R1, = SYSCTL_RCGCGPIO
			LDR 	R0, [R1]
			ORR 	R0, R0, #0x10
			STR 	R0, [R1]

			; Check Port Ready Status
			LDR 	R1, = SYSCTL_PRGPIO
CHK_PRT		LDR 	R0, [R1]
			ANDS  	R0, #0x10
			BEQ 	CHK_PRT
			
			; Set Directions
			LDR 	R1, =GPIO_PORTE_DIR_R	; Inputs: E2, E3 | Outputs: E4(High), E5(Open Drain)
			MOV 	R0, #0x30
			STR 	R0, [R1]

			; Enable Digital Port
			LDR 	R1, =GPIO_PORTE_DEN_R
			MOV 	R0, #0x30
			STR 	R0, [R1]
			
			; Enable Open Drain For E5
			LDR 	R1, =GPIO_PORTE_OD_R
			MOV 	R0, #0x20
			STRB 	R0, [R1]
			
			; Set Analog
			LDR 	R1, =GPIO_PORTE_AMSEL_R
			MOV 	R0, #0x0C 				; PE2 + PE3 (IN1 + IN0)
			STR 	R0, [R1]
			
			; Set as Analog ADC In
			LDR 	R1, =GPIO_PORTE_AFSEL_R
			MOV 	R0, #0x0C
			STR 	R0, [R1]
			
			; Set E4 High & E5 Open Drain Active
			LDR 	R1, =GPIO_PORTE_DATA_OUT
			MOV 	R0, #0x10
			STR 	R0, [R1]
			
			POP 	{R0, R1}
			BX 		LR
			ENDP
				
			ALIGN
			END