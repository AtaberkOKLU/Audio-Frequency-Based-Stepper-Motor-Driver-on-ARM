GPIO_PORTF_DATA_ALL	EQU 0x400253FC
GPIO_PORTF_DATA_OUT	EQU 0x40025038
GPIO_PORTF_DIR_R 	EQU 0x40025400
GPIO_PORTF_AFSEL_R 	EQU 0x40025420
GPIO_PORTF_DEN_R 	EQU 0x4002551C
GPIO_PORTF_PUR_R 	EQU 0x40025510
GPIO_PORTF_LOCK		EQU 0x40025520
GPIO_PORTF_CR 		EQU 0x40025524
GPIO_PORTF_IEV 		EQU 0x4002540C
GPIO_PORTF_IMSK 	EQU 0x40025410
GPIO_PORTF_MIS 		EQU 0x40025418
GPIO_PORTF_ICR 		EQU 0x4002541C
SYSCTL_RCGCGPIO		EQU 0x400FE608
SYSCTL_PRGPIO 		EQU 0x400FEA08


; SW1 SW2 	: Inputs
; LED R/G/B : Outputs

			AREA INIT_GPIOF_CODE, CODE, READONLY
			THUMB
			EXPORT INIT_GPIOF
				
INIT_GPIOF	PROC
			PUSH 	{R0, R1}
			; Turn On Port F Clock
			LDR 	R1, = SYSCTL_RCGCGPIO
			LDR 	R0, [R1]
			ORR 	R0, R0, #0x20
			STR 	R0, [R1]

			; Check Port Ready Status
			LDR 	R1, = SYSCTL_PRGPIO
CHK_PRT		LDR 	R0, [R1]
			ANDS  	R0, #0x20
			BEQ 	CHK_PRT
			
			; UnLock GPIO-F:PE0
			LDR 	R1, =GPIO_PORTF_LOCK
			LDR 	R0, =0x4C4F434B
			STR 	R0, [R1]
			
			; Commit PE0
			LDR 	R1, =GPIO_PORTF_CR
			MOV 	R0, #0x01
			STRB 	R0, [R1]
			
			; Set Directions
			LDR 	R1, =GPIO_PORTF_DIR_R
			MOV 	R0, #0x0E
			STR 	R0, [R1]
			
			; Set as Regular GPIO
			LDR 	R1, =GPIO_PORTF_AFSEL_R
			MOV 	R0, #0x00
			STR 	R0, [R1]

			; Enable Digital Port
			LDR 	R1, =GPIO_PORTF_DEN_R
			MOV 	R0, #0x1F
			STR 	R0, [R1]
			
			; Set Input SW pin as Pull Up
			LDR 	R1, =GPIO_PORTF_PUR_R
			MOV 	R0, #0x11
			STR 	R0, [R1]
			
			; Set Output Pins as High (LEDs are On)
			LDR 	R1, =GPIO_PORTF_DATA_OUT
			MOV 	R0, #0x0E
			STR		R0, [R1]
			
			POP 	{R0, R1}
			BX 		LR
			ENDP
				
			ALIGN
			END